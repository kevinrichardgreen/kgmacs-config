#+TITLE: Configuration file for Kevin Green's emacs

* Fundamentals/defaults

Frame titles - incorporate buffer names.

#+BEGIN_SRC emacs-lisp
  (setq frame-title-format '("emacs: %b [%m]"))
#+END_SRC

Sensible defaults, courtesy of [[https://github.com/hrs/sensible-defaults.el][Harry R. Schwartz]].

#+BEGIN_SRC emacs-lisp
  (load-file (expand-file-name "sensible-defaults.el" user-emacs-directory))

  (sensible-defaults/bind-commenting-and-uncommenting)
  (sensible-defaults/bind-keys-to-change-text-size)

  (sensible-defaults/increase-gc-threshold)
  (sensible-defaults/delete-trailing-whitespace)
  (sensible-defaults/treat-camelcase-as-separate-words)
  (sensible-defaults/make-scripts-executable)
  (sensible-defaults/single-space-after-periods)
  (sensible-defaults/offer-to-create-parent-directories-on-save)
  (sensible-defaults/ensure-that-files-end-with-newline)
  (sensible-defaults/quiet-startup)
  (sensible-defaults/make-dired-file-sizes-human-readable)
  (sensible-defaults/shorten-yes-or-no)
  (sensible-defaults/always-highlight-code)
  (sensible-defaults/refresh-buffers-when-files-change)
  (sensible-defaults/show-matching-parens)
  (setq show-paren-style 'expression)     ;; NOTE this goes well with above
  (sensible-defaults/flash-screen-instead-of-ringing-bell)
  (sensible-defaults/yank-to-point-on-mouse-click)
#+END_SRC

Font.  (set in a way that will work with emacsclient frames)

#+BEGIN_SRC emacs-lisp
(setq default-frame-alist '((font . "Monospace-8")))
#+END_SRC

Handle the top bars.

#+BEGIN_SRC emacs-lisp
  (tool-bar-mode -1)
  (menu-bar-mode 1)
#+END_SRC

Line & column numbers

#+BEGIN_SRC emacs-lisp
  (line-number-mode 1)
  (column-number-mode 1)
#+END_SRC

Disable automatic 'electric' indent (it conflicts with other things!)

#+BEGIN_SRC emacs-lisp
  (electric-indent-mode 0)
#+END_SRC

Smoother scrolling, scroll bar handling

#+BEGIN_SRC emacs-lisp
  (set-scroll-bar-mode nil) ; position of scroll bar
  (setq                     ; smoother scrolling
    scroll-margin 1                   ;; how far from edge before scroll
    scroll-conservatively 100    ;; scroll nicely
    scroll-preserve-screen-position 1) ;; maintain position when pgup/dwn
#+END_SRC

An awesome tool for traversing the undo/redo tree.

#+BEGIN_SRC emacs-lisp
  (use-package undo-tree
    :config
    (global-undo-tree-mode))
#+END_SRC

Remember position in a file: keeps ~/ clean,  activated on all buffers

#+BEGIN_SRC emacs-lisp
  (use-package saveplace
    :init (save-place-mode))
#+END_SRC

Line numbering - relative line numbers in programming and text modes only.

#+BEGIN_SRC emacs-lisp
  (use-package linum-relative
    :config
    (setq relative-line-numbers-motion-function 'forward-visible-line)
    (setq linum-relative-current-symbol "")
    (add-hook 'text-mode-hook 'linum-relative-mode)
    (add-hook 'prog-mode-hook 'linum-relative-mode))
#+END_SRC

Global line highlighting

#+BEGIN_SRC emacs-lisp
  (global-hl-line-mode 1)
#+END_SRC


Allow certain functions that are disabled by default.

#+BEGIN_SRC emacs-lisp
  (put 'downcase-region 'disabled nil)
  (put 'dired-find-alternate-file 'disabled nil)
  (put 'set-goal-column 'disabled nil)
  (put 'scroll-left 'disabled nil)
  (put 'scroll-right 'disabled nil)
#+END_SRC

Scroll buffer in place: [[http://stackoverflow.com/questions/8993183/emacs-scroll-buffer-not-point][StackOverflow]]

#+BEGIN_SRC emacs-lisp
  (defun scroll-down-in-place (n)
    "Scroll the buffer down, keeping the point in place."
    (interactive "p")
    (next-line n)
    (unless (eq (window-start) (point-min))
      (scroll-down n)))

  (defun scroll-up-in-place (n)
    "Scroll the buffer up, keeping the point in place."
    (interactive "p")
    (previous-line n)
    (unless (eq (window-end) (point-max))
      (scroll-up n)))

  (global-set-key "\M-n" 'scroll-up-in-place)
  (global-set-key "\M-p" 'scroll-down-in-place)
#+END_SRC

Use spaces in ~align-regexp~ instead of tabs. - uses old advice syntax

#+BEGIN_SRC emacs-lisp
  (defadvice align-regexp (around align-regexp-with-spaces activate)
    (let ((indent-tabs-mode nil))
      ad-do-it))
#+END_SRC

Browsing the kill ring

#+BEGIN_SRC emacs-lisp
  (use-package browse-kill-ring+
    :config
    (browse-kill-ring-default-keybindings))
#+END_SRC

Backup settings. Courtesy of [[http://stackoverflow.com/a/20824625][Stack Overflow]]

#+BEGIN_SRC emacs-lisp
  (setq
     backup-by-copying t      ; don't clobber symlinks
     delete-old-versions t
     kept-new-versions 10
     kept-old-versions 0
     version-control t)       ; use versioned backups
  (setq vc-make-backup-files t)
  ;; Default and per-save backups go here:
  (setq backup-directory-alist '(("" . "~/.emacs_backups/per-save")))

  (defun force-backup-of-buffer ()
    ;; Make a special "per session" backup at the first save of each
    ;; emacs session.
    (when (not buffer-backed-up)
      ;; Override the default parameters for per-session backups.
      (let ((backup-directory-alist '(("" . "~/.emacs_backups/per-session")))
            (kept-new-versions 3))
        (backup-buffer)))
    ;; Make a "per save" backup on each save.  The first save results in
    ;; both a per-session and a per-save backup, to keep the numbering
    ;; of per-save backups consistent.
    (let ((buffer-backed-up nil))
      (backup-buffer)))
  (add-hook 'before-save-hook  'force-backup-of-buffer)
#+END_SRC

Use ~ibuffer~ as default over ~list-buffers~. Group configuration from [[https://cestlaz.github.io/posts/using-emacs-34-ibuffer-emmet/#.WiYJuOmnHRY][Using emacs 34]].

#+BEGIN_SRC emacs-lisp
  (use-package ibuffer
    :config
    (global-set-key (kbd "C-x C-b") 'ibuffer)
    (setq ibuffer-saved-filter-groups
          (quote (("default"
                   ("dired" (mode . dired-mode))
                   ("org" (name . "^.*org$"))
                   ("programming" (or
                                   (mode . python-mode)
                                   (mode . c++-mode)
                                   (mode . c-mode)
                                   (mode . fortran-mode)))
                   ("TeX" (or
                             (name . "^.*tex$")
                             (name . "^.*bib$")))
                   ("shell" (or (mode . eshell-mode) (mode . shell-mode)))
                   ("mu4e" (name . "\*mu4e\*"))
                   ("Magit" (name . "\*magit\*"))
                   ("web" (or (mode . web-mode) (mode . js2-mode)))
                   ("emacs" (or
                             (name . "^\\*scratch\\*$")
                             (name . "^\\*Messages\\*$")))
                   ))))
    (add-hook 'ibuffer-mode-hook
              (lambda ()
                (ibuffer-auto-mode 1)
                (ibuffer-switch-to-saved-filter-groups "default")))

    ;; don't show these
                                          ;(add-to-list 'ibuffer-never-show-predicates "zowie")
    ;; Don't show filter groups if there are no buffers in that group
    (setq ibuffer-show-empty-filter-groups nil)

    )
#+END_SRC

System cut and paste mapped to <f-> keys.

#+BEGIN_SRC emacs-lisp
  (global-set-key [f5] 'clipboard-kill-region)    ; cut
  (global-set-key [f6] 'clipboard-kill-ring-save) ; copy
  (global-set-key [f7] 'clipboard-yank)    ; paste
#+END_SRC

Don't allow backgrounding of emacs.

#+BEGIN_SRC emacs-lisp
  (global-unset-key (kbd "C-z"))
#+END_SRC

** Web

Ask for eww open

#+BEGIN_SRC emacs-lisp
  (defalias 'gk-urls-external-browser 'browse-url-xdg-open)

  (defun gk-browse-url (&rest args)
    "Prompt for whether or not to browse with EWW, if no browse
  with external browser."
    (apply
     (if (y-or-n-p "Browse with EWW? ")
         'eww-browse-url
       'gk-urls-external-browser)
     args))

  (setq browse-url-browser-function #'gk-browse-url)
#+END_SRC

** Compilation

Global bindings for fast compile, auto-scroll of compilation window.

#+BEGIN_SRC emacs-lisp
  (global-set-key [f9] 'compile)
  (global-set-key [f10] 'recompile)
  (global-set-key [f12] 'gdb)
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (setq compilation-scroll-output t)
  (setq compilation-auto-jump-to-first-error t)
  (setq compilation-skip-threshold 2) ; don't worry about warnings!
#+END_SRC

* English

~flyspell~ for text files and code files
#+BEGIN_SRC emacs-lisp
  (add-hook 'text-mode-hook 'flyspell-mode)
  (add-hook 'prog-mode-hook 'flyspell-prog-mode)
#+END_SRC

* Files and directories

Open certain directories in dired.

#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "C-c o")
                  (lambda () (interactive) (find-file "~/Dropbox/Documents/")))
  (global-set-key (kbd "C-c r")
                  (lambda () (interactive) (find-file "~/repositories/")))
#+END_SRC

~Wdired~ mode.  Allow changing permissions.

#+BEGIN_SRC emacs-lisp
  (setq wdired-allow-to-change-permissions t)
#+END_SRC

~dired+~ for some more powerful behaviour

#+BEGIN_SRC emacs-lisp
  (use-package dired+
    :config
    (require 'dired+)
    (setq global-dired-hide-details-mode nil) ;; show details by default
    (setq dired-listing-switches "-lh")           ;; don't list all by default
    )
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (use-package dired-sidebar
    :commands (dired-sidebar-toggle-sidebar)
    :config
    (setq dired-sidebar-use-all-the-icons nil))
#+END_SRC

Open files and goto lines like we see from g++ etc. i.e. file:line.
(Also useful for noweb source if formatted like this)

Courtesy of [[https://stackoverflow.com/a/3141456/1899759][StackOverflow]].

#+BEGIN_SRC emacs-lisp
  (defadvice find-file (around find-file-line-number
                               (filename &optional wildcards)
                               activate)
    "Turn files like file.cpp:14 into 'open file.cpp and go to the 14-th line.'"
    (save-match-data
      (let* ((matched (string-match "^\\(.*\\):\\([0-9]+\\):?$" filename))
             (line-number (and matched
                               (match-string 2 filename)
                               (string-to-number (match-string 2 filename))))
             (filename (if matched (match-string 1 filename) filename)))
        ad-do-it
        (when line-number
          ;; goto-line is for interactive use
          (goto-char (point-min))
          (forward-line (1- line-number))))))
#+END_SRC

* Ivy/Swiper/Counsel & Avy

Just as intuitive as IDO, seems lighter weight than helm.

#+BEGIN_SRC emacs-lisp
  (use-package ivy :demand
    :config
    (global-set-key "\C-s" 'swiper)
    (global-set-key "\M-x" 'counsel-M-x)
    (setq ivy-use-virtual-buffers t
          ivy-count-format "%d/%d "
          enable-recursive-minibuffers t)
    (ivy-mode 1)
    ;; configure regexp engine.
    (setq ivy-re-builders-alist
          ;; allow input not in order
          '((t   . ivy--regex-ignore-order)))
  )
#+END_SRC

Flyspell ivy correction completion.

#+BEGIN_SRC emacs-lisp
  (use-package flyspell-correct-ivy
    :config
    (define-key flyspell-mode-map (kbd "C-'") 'flyspell-correct-previous-word-generic)
  )
#+END_SRC

Better buffer formatting in ~ivy-switch-buffer~

#+BEGIN_SRC emacs-lisp
  (use-package ivy-rich
    :after ivy
    :config
    (ivy-set-display-transformer 'ivy-switch-buffer 'ivy-rich-switch-buffer-transformer)
    (setq ivy-virtual-abbreviate 'full
          ivy-rich-switch-buffer-align-virtual-buffer t
          ivy-rich-path-style 'abbrev)
  )
#+END_SRC

Avy for fast jumping anywhere visible.

#+BEGIN_SRC emacs-lisp
  (use-package avy
    :config
    (global-set-key (kbd "C-:") 'avy-goto-char)
    (global-set-key (kbd "C-;") 'avy-goto-char-2)
    (global-set-key (kbd "M-g f") 'avy-goto-line)
    (global-set-key (kbd "M-g w") 'avy-goto-word-1))
#+END_SRC

* Color themes

I use =moe-theme=, which is actually much more than just a color
theme.  It's more of an overall style theme.

#+BEGIN_SRC emacs-lisp
  (use-package moe-theme
    :init
    (setq calendar-latitude +52)
    (setq calendar-longitude -106)
    (setq moe-theme-resize-org-title '(1.3 1.1 1.1 1.1 1.0 1.0 1.0 1.0 1.0))
    :config
    (moe-theme-set-color 'red)
    (moe-dark)
    )
#+END_SRC

* Org mode

=org-mode= is arguably the workhorse of emacs.  Making sure it behaves
as you wish is critical to having a good emacs experience.

#+BEGIN_SRC emacs-lisp
  (use-package org
  :config
    (global-set-key (kbd "C-c l") 'org-store-link)
    (global-set-key (kbd "C-c a") 'org-agenda)
    (global-set-key (kbd "C-c c") 'org-capture)

    (setq org-use-speed-commands t)
    (setq org-fontify-whole-heading-lines t)
    (setq org-html-validation-link nil)
    (setq org-startup-with-inline-images t)
    (setq org-startup-with-latex-preview t)
    (setq org-image-actual-width 400)
    (setq org-startup-indented t)
    (setq org-hide-emphasis-markers t)
    (setq org-src-fontify-natively t)
    (setq org-format-latex-options (plist-put org-format-latex-options :scale 2.0))
    ;; Fancy bullets
    (use-package org-bullets
      :config
      (add-hook 'org-mode-hook (lambda () (org-bullets-mode 1)))
      (setq org-hide-leading-stars t))
    ;; References inside org-mode documents
    (use-package org-ref
      :config
      (with-eval-after-load 'org
        (setq org-ref-notes-directory "~/Dropbox/Documents/notes/reading"
              org-ref-bibliography-notes "~/Dropbox/Documents/index.org"
              org-ref-default-bibliography '("~/Dropbox/Documents/index.bib")
              org-ref-pdf-directory '("~/Dropbox/Documents/pdf_books/" "~/Dropbox/Documents/pdf_papers"))
        (setq helm-bibtex-bibliography "~/Dropbox/Documents/index.bib"
              helm-bibtex-library-path '("~/Dropbox/Documents/pdf_books/" "~/Dropbox/Documents/pdf_papers")
              helm-bibtex-notes-path "~/Dropbox/Documents/notes/reading"
              bibtex-completion-bibliography "~/Dropbox/Documents/index.bib")))
    (use-package ivy-bibtex)
    ;; Managing bibtex entries
    (use-package bibtex-utils
      :config
      (setq bu-bibtex-fields-ignore-list '("keywords" "abstract" "file" "issn" "annote"))
      (setq bibtex-align-at-equal-sign t)
      (add-hook 'bibtex-mode-hook (lambda () (set-fill-column 2000))))
    ;; Some export modes/options
    (setq org-latex-pdf-process (list "latexmk -shell-escape -bibtex -f -pdf %f"))
    (use-package htmlize)  ; for source code block syntax highlighting
    (use-package ox-twbs)  ; Twitter-bootstrap formatted html
    (use-package ox-trac)  ; export to trac-wiki to paste into tickets
    (use-package ox-reveal
      :config
      (setq org-reveal-root "http://cdn.jsdelivr.net/reveal.js/3.0.0/")
      (setq org-reveal-mathjax t))
    ;; sync with Google Calendar
    (setq package-check-signature nil)
    (use-package org-gcal
      :config
      (load "~/Dropbox/Documents/gtd/gcal-details")
      ;; Special key for gcal sync and refresh
      (require 'org-agenda)
      (defun org-agenda-gcal-sync-and-redo-all (&optional all)
        (interactive "P")
        (org-gcal-sync)
        (org-agenda-redo-all))
      (define-key org-agenda-mode-map "G" 'org-agenda-gcal-sync-and-redo-all)
      )
      ;; (add-hook 'org-capture-after-finalize-hook (lambda () (org-gcal-sync) ))
    ;; GTD things
    (setq org-agenda-files '("~/Dropbox/Documents/gtd/gcal.org"
                             "~/Dropbox/Documents/gtd/inbox.org"
                             "~/Dropbox/Documents/gtd/work.org"
                             "~/Dropbox/Documents/gtd/personal.org"))
    (setq org-refile-targets '(("~/Dropbox/Documents/gtd/work.org" :maxlevel . 1)
                               ("~/Dropbox/Documents/gtd/personal.org" :maxlevel . 1)
                               ("~/Dropbox/Documents/gtd/someday.org" :level . 1)))

    (use-package org-mu4e
      :after mu4e
      :config
      (setq org-mu4e-link-query-in-headers-mode nil))
    ;; Capture templates
    (setq org-capture-templates
          '(
            ("a" "Appointment" entry (file  "~/Dropbox/Documents/gtd/gcal.org" )
             "* %?\n\n%^T\n\n")
            ("f" "File-todo [inbox]" entry (file  "~/Dropbox/Documents/gtd/inbox.org" )
             "* TODO %?\n\n%a\n")
            ("t" "Todo [inbox]" entry
             (file "~/Dropbox/Documents/gtd/inbox.org")
             "* TODO %i%?")))
    (setq org-todo-keywords '((sequence "TODO(t)" "WAITING(w)" "IN-PROGRESS(p)" "|" "DONE(d)" "CANCELLED(c)")))

    (setq org-refile-use-outline-path nil)
    (setq org-outline-path-complete-in-steps t)
    (setq org-refile-allow-creating-parent-nodes 'confirm)
    (setq org-agenda-start-on-weekday nil)
    (setq org-agenda-todo-ignore-scheduled "all")
    (setq org-agenda-skip-scheduled-if-done t)
    (setq org-deadline-warning-days 5)
    (setq org-log-done 'time)
    (setq org-use-property-inheritance '("CATEGORY"))
    (setq org-agenda-window-setup 'reorganize-frame)
    (setq org-agenda-span 'day)
    ;; (add-to-list
    ;;  'ivy-completing-read-handlers-alist
    ;;  '(org-capture-refile . completing-read-default))
    )
#+END_SRC

* She sells sea shells

Well, she is a /TRAMP/...

TRAMP connections use persistent ssh
+ default to using rsync over ssh
= super fast file interaction on remote servers.

#+BEGIN_SRC emacs-lisp
  (setq tramp-ssh-controlmaster-options
        (concat
         "-o ControlPath=/tmp/ssh-TRAMP-ControlPath-%%r@%%h:%%p "
         "-o ControlMaster=auto -o ControlPersist=yes"))
  (setq tramp-default-method "rsync")
#+END_SRC

Open an external ~gnome-terminal~ in the remote directory of a TRAMP
buffer. This was pieced together using  elements from:
- https://emacs.stackexchange.com/questions/18903/tramp-and-dired-initial-default-directory-dired-directory
- https://stackoverflow.com/questions/23164073/run-a-shell-command-from-a-specific-directory-in-emacs
- https://unix.stackexchange.com/questions/373186/open-gnome-terminal-window-and-execute-2-commands

#+BEGIN_SRC emacs-lisp
  (defun open-gnome-terminal-remote ()
    "Opens a gnome-terminal at location of current TRAMP buffer
  - ssh to machine using the existing ssh-TRAMP socket
  - fails if not in a TRAMP buffer
  - gnome-terminal closes when remote shell is terminated
    - but shared ssh connection remains"
    (interactive)
    (let* ( ;; break current TRAMP directory into pieces
           (remote-list (split-string (substring-no-properties default-directory) ":"))
           (remote-protocol (nth 0 remote-list))
           (remote-system   (nth 1 remote-list))
           (remote-dir      (nth 2 remote-list))
           ;; set directory to home on local machine
           (default-directory (getenv "HOME"))
           ;; map the %% -> % to be used in our command string
           (ssh-shared-opts (format tramp-ssh-controlmaster-options))
           ;; multiple leves of escaped quotes to avoid single quotes...
           (gt-command (concat
                        "gnome-terminal -e \"sh -c \\\"ssh "
                        ssh-shared-opts " -t "
                        remote-system
                        " \\\\\\\"cd " remote-dir
                        "; exec $SHELL\\\\\\\"\\\"\"")))
      (shell-command gt-command)))

  (defun open-gnome-terminal ()
    "Open a gnome-terminal session in current directory"
    (interactive)
    (if (file-remote-p default-directory)
        (open-gnome-terminal-remote)
      (shell-command "gnome-terminal")))
#+END_SRC

Open a new ~multi-term~ in the remote directory of a TRAMP buffer.

#+BEGIN_SRC emacs-lisp
  (defun open-multi-term-remote ()
    "Opens a multi-term at location of current TRAMP buffer
    - ssh to machine using the existing ssh-TRAMP socket
    - ssh command fails if not in a TRAMP buffer"
    (interactive)
    (multi-term)
    (let* ( ;; break current TRAMP directory into pieces
           (remote-list (split-string (substring-no-properties default-directory) ":"))
           (remote-protocol (nth 0 remote-list))
           (remote-system   (nth 1 remote-list))
           (remote-dir      (nth 2 remote-list))
           ;; set directory to home on local machine
           (default-directory (getenv "HOME"))
           ;; map the %% -> % to be used in our command string
           (ssh-shared-opts (format tramp-ssh-controlmaster-options))
           ;; multiple leves of escaped quotes to avoid single quotes...
           (ssh-command (concat
                         "ssh "
                         ssh-shared-opts " -t "
                         remote-system
                         " \"cd " remote-dir
                         "; exec $SHELL\"")))
      (term-send-raw-string ssh-command)
      (term-send-return)
      (term-send-raw-string "clear")
      (term-send-return)))

  (defun open-multi-term ()
    "Open a mulit-term session in current directory"
    (interactive)
    (if (file-remote-p default-directory)
        (open-multi-term-remote)
      (multi-term)))
#+END_SRC

Only use =bash= for shells.  Useful for remote shell through TRAMP

#+BEGIN_SRC emacs-lisp
  (setq explicit-shell-file-name "/bin/bash")
#+END_SRC

A prettified eshell.

#+BEGIN_SRC emacs-lisp
  (setq eshell-cmpl-cycle-completions nil)

  (defmacro with-face (str &rest properties)
    `(propertize ,str 'face (list ,@properties)))

  (defun fancy-eshell-prompt ()
    "Fancy looking eshell."
    (let ((header-bg "#BBB")
          (header-fg "#000")
          (time-fg   "#C33")
          (user-fg   "#0A2")
          (host-fg   "#0A2")
          (prompt-fg "#0A2"))
      (concat
                                          ; Colored user and hostname
       (with-face user-login-name :foreground user-fg)
       "@"
       (with-face system-name :foreground host-fg)
                                          ; Current directory
       (with-face (concat (eshell/pwd) " ") :background header-bg :foreground header-fg)
                                          ; Current time
       (with-face (format-time-string "(%Y-%m-%d %H:%M:%S) " (current-time)) :background header-bg :foreground time-fg)
                                          ; Version control info of current directory
       (with-face
        (or (ignore-errors (format "(%s)" (vc-responsible-backend default-directory))) "")
        :background header-bg :foreground user-fg)
                                          ; NEW LINE FOR PROMPT
       (with-face "\n" :background header-bg)
                                          ; root prompt should always be red!
       (if (= (user-uid) 0)
           (with-face " #" :foreground "red")
         (with-face " $" :foreground prompt-fg))
       " ")))
  (setq eshell-prompt-function 'fancy-eshell-prompt)
  (setq eshell-highlight-prompt nil)
#+END_SRC

For some reason, my TERM env is set to dumb when ~shell~ is run in emacs...
I want color!

#+BEGIN_SRC emacs-lisp
  (defun my-shell-mode-hook ()
    (process-send-string (get-buffer-process (current-buffer))
                         "export TERM=ansi\n"))
  (add-hook 'shell-mode-hook 'my-shell-mode-hook)
#+END_SRC

* Version control

** Magit

Probably the best interface to git. It makes me want to code more just
so I can commit more.

#+BEGIN_SRC emacs-lisp
    (use-package magit
      :config
      (global-set-key (kbd "C-x g") 'magit-status)
      (global-set-key (kbd "C-x M-g") 'magit-dispatch-popup)
  )
#+END_SRC

* Languages/Programming

GDB many open windows for debugging.  This is useful for seeing many things
about your debug session, but does screw the window layout for a bit.

- I'm curious to see how this interacts with frames-only-mode
  - turns out it behaves as the original! (split the current frame
    into 6 windows)

#+BEGIN_SRC emacs-lisp
  (setq gdb-many-windows t)
#+END_SRC

Ensure ~minimap-mode~ is available. Don't necessarily turn it on all the
time though.

#+BEGIN_SRC emacs-lisp
  (use-package minimap)
#+END_SRC

** C/C++

#+BEGIN_SRC emacs-lisp
  (add-hook 'c-mode-hook 'counsel-gtags-mode)
  (add-hook 'c++-mode-hook 'counsel-gtags-mode)

  ()
  (use-package counsel-gtags
    :config
    (define-key counsel-gtags-mode-map (kbd "M-t") 'counsel-gtags-find-definition)
    (define-key counsel-gtags-mode-map (kbd "M-r") 'counsel-gtags-find-reference)
    (define-key counsel-gtags-mode-map (kbd "M-s") 'counsel-gtags-find-symbol)
    (define-key counsel-gtags-mode-map (kbd "M-,") 'counsel-gtags-go-backward))
#+END_SRC
** Haskell

Useful for managing my xmonad setup.

#+BEGIN_SRC emacs-lisp
  (use-package haskell-mode)
#+END_SRC

** Elisp

Useful for customizing the shit of emacs. Close up those damn parens
in all lisp modes.

#+BEGIN_SRC emacs-lisp
  (use-package paredit
    :config
    (autoload 'enable-paredit-mode "paredit" "Turn on pseudo-structural editing of Lisp code." t)
    (add-hook 'emacs-lisp-mode-hook       #'enable-paredit-mode)
    (add-hook 'eval-expression-minibuffer-setup-hook #'enable-paredit-mode)
    (add-hook 'ielm-mode-hook             #'enable-paredit-mode)
    (add-hook 'lisp-mode-hook             #'enable-paredit-mode)
    (add-hook 'lisp-interaction-mode-hook #'enable-paredit-mode)
    (add-hook 'scheme-mode-hook           #'enable-paredit-mode))
#+END_SRC

** Matlab

=matlab-mode= for syntax highlighting in .m files, as well as
=matlab-shell= that can be used within emacs.

A nice feature of this is that you can evaluate execution groups (is
that what they're called in Matlab?) right from a .m file.

#+BEGIN_SRC emacs-lisp
  (use-package matlab
    :config
    (matlab-cedet-setup)
    (add-to-list
     'auto-mode-alist
     '("\\.m$" . matlab-mode))
    (setq matlab-indent-function t)
    (setq matlab-shell-command "matlab")
    (setq matlab-completion-technique 'increment))
#+END_SRC

** Maple

~maplev-mode~ for basic syntax highlighting in maple text files. /Note the
file extensions used./

I downloaded this somewhere from the interwebz a long time ago... I
don't see a melpa package for it.

#+BEGIN_SRC emacs-lisp
  (add-to-list 'load-path (concat user-emacs-directory "maple"))
  (require 'maplev)
  (autoload 'maplev-mode "maplev" "Maple editing mode" 'interactive)
  (setq auto-mode-alist
        (cons (cons (concat "\\." (regexp-opt '("mpl" "maple") t)
                            "$")
                    'maplev-mode)
              auto-mode-alist))
#+END_SRC

** LaTeX

Ok, so this is a big one too.  AUCTeX+RefTex for LaTeX editing.

#+BEGIN_SRC emacs-lisp
    (use-package tex
      :defer t
      :config
      (setq LaTeX-eqnarray-label "eq")
      (setq LaTeX-equation-label "eq")
      (setq LaTeX-figure-label "fig")
      (setq LaTeX-table-label "tab")
      (setq LaTeX-myChapter-label "chap")
      (setq TeX-newline-function 'reindent-then-newline-and-indent)
      (setq LaTeX-section-hook
            '(LaTeX-section-heading
              LaTeX-section-title
              LaTeX-section-toc
              LaTeX-section-section
              LaTeX-section-label))
      (setq TeX-parse-self t)
      (setq TeX-auto-save t)
      (setq TeX-source-correlate-mode t)
      (setq LaTeX-electric-left-right-brace t)
      (add-hook 'LaTeX-mode-hook 'flyspell-mode)
      (add-hook 'LaTeX-mode-hook 'flyspell-buffer)
      ;; And configure reftex here as well
      (use-package reftex
        :config
        (add-hook 'LaTeX-mode-hook 'turn-on-reftex) ; with AUCTeX LaTeX mode
        (setq reftex-plug-into-AUCTeX t)
  ;      (setq reftex-toc-split-windows-horizontally t)
        (setq reftex-toc-include-labels t)
        (setq reftex-toc-include-file-boundaries t)
        (setq reftex-auto-recenter-toc t)
        (setq reftex-idle-time 0.5))
      ;; latexmk support within auctex
      (use-package auctex-latexmk
        :config
        (auctex-latexmk-setup))
      ;; Use pdf-tools to open PDF files
      (setq TeX-view-program-selection '((output-pdf "PDF Tools"))
            TeX-source-correlate-start-server t)
      ;; Update PDF buffers after successful LaTeX runs
      (add-hook 'TeX-after-TeX-LaTeX-command-finished-hook
                #'TeX-revert-document-buffer)
      (add-hook 'TeX-mode-hook '(lambda () (setq TeX-command-default "LatexMk")))
      )
#+END_SRC

** Python

=elpy= has some nice features. Want to use Ipython.

#+BEGIN_SRC emacs-lisp
  (use-package elpy
    :config
    (elpy-enable))
;    (elpy-use-ipython))
#+END_SRC

** Gnuplot

Get the ~gnuplot~ script editing mode, and comint mode;

#+BEGIN_SRC emacs-lisp
  (use-package gnuplot
    :config
    (add-to-list 'auto-mode-alist '("\\.gplt" . gnuplot-mode))
    (gnuplot-inline-display-mode))
#+END_SRC

** Noweb

Configuration for doing noweb stuff.  Uses polymode

#+BEGIN_SRC emacs-lisp
  (use-package polymode
    :config
    (add-to-list 'auto-mode-alist '("\\.nw" . poly-noweb-mode))
    (add-hook 'poly-noweb-mode-hook 'flyspell-mode)) ; enable flyspell
#+END_SRC

Creating a new noweb chunk. (Does this global binding interfere with
anything? I could perhaps make this for only in ~poly-noweb-mode~)

#+BEGIN_SRC emacs-lisp
  (defun create-new-chunk (chunk-display-name)
    "Create a new noweb chunk with display name from prompt"
    (interactive "sChunk display name (space and . converted to -): ")
    (let* ((chunk-latex-name
            (replace-regexp-in-string " " "-" (replace-regexp-in-string
                                               (regexp-quote ".") "-"
                                               chunk-display-name))))
      (insert "%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%\n"
              (concat "\\begin{chnk}{" chunk-latex-name "}\n")
              (concat "<<" chunk-display-name ">>=\n\n")
              "@ %def\n"
              "\\end{chnk}\n"
              "%-------------------------------------------------------------------------------\n")
      (previous-line 4)))  ; go back to code entry point before exit
  (global-set-key (kbd "C-c h") 'create-new-chunk)
#+END_SRC

* Pdf viewing

=pdf-tools= is a far superior pdf viewer than the default DocView mode
that comes with emacs.

#+BEGIN_SRC emacs-lisp
  (use-package pdf-tools
    :config
    (pdf-tools-install)
    (setq-default pdf-view-display-size 'fit-page)
    (setq pdf-view-resize-factor 1.1)              ;; finer control
    ;; Shorter keystrokes for annotations
    (define-key pdf-view-mode-map (kbd "h") 'pdf-annot-add-highlight-markup-annotation)
    (define-key pdf-view-mode-map (kbd "t") 'pdf-annot-add-text-annotation)
    (define-key pdf-view-mode-map (kbd "D") 'pdf-annot-delete)
    (use-package org-pdfview))
#+END_SRC

=interleave-mode= is the perfect way to take notes on pdf documents. My opinion on this has changed... I prefer annotations now.

#+BEGIN_SRC emacs-lisp
;;  (use-package interleave)
#+END_SRC
* Email

** mu4e

Using ~mu4e~. *Setup is for UofS and Gmail accounts.*
- ~offlineimap~ is used for obtaining email (Install from [[https://github.com/OfflineIMAP/offlineimap][GitHub]])
- ~mu~ for indexing mail (from [[https://github.com/djcb/mu][GitHub]])
- ~msmtp~ for sending mail (Ubuntu repo is good enough)

Useful refs:
- [[http://cachestocaches.com/2017/3/complete-guide-email-emacs-using-mu-and-/]] :: decent
     for overall setup, with some quirks due to using a mix of the
     'old' way of handling multiple accounts combined with the new
     contexts.
- [[https://notanumber.io/2016-10-03/better-email-with-mu4e/]] :: best
     description of using the newer style of contexts for multiple
     accounts and msmtp for sending.

#+BEGIN_SRC emacs-lisp
  (add-to-list 'load-path "/usr/local/share/emacs/site-lisp/mu4e")
  (use-package mu4e
    :commands (mu4e make-mu4e-bookmark)
    :init
    (defvar mu4e-bookmarks ;; define before mu4e gets a chance to!
      `(,(make-mu4e-bookmark
          :name  "All Unread messages"
          :query "flag:unread AND NOT flag:trashed"
          :key ?u)
        ,(make-mu4e-bookmark
          :name  "UofS Unread messages"
          :query "flag:unread AND NOT flag:trashed AND maildir:/UofS/INBOX"
          :key ?U)
        ,(make-mu4e-bookmark
          :name  "Gmail Unread messages"
          :query "flag:unread AND NOT flag:trashed AND maildir:/Gmail/INBOX"
          :key ?G)
        ,(make-mu4e-bookmark
          :name "Today's messages"
          :query "date:today..now"
          :key ?t)
        ,(make-mu4e-bookmark
          :name "Last 7 days"
          :query "date:7d..now"
          :key ?w)
        ,(make-mu4e-bookmark
          :name "Messages with images"
          :query "mime:image/*"
          :key ?p)))
    :config
    (setq mail-user-agent 'mu4e-user-agent) ; use mu4e as default
    (setq mu4e-maildir "~/Maildir")
    ;; Sets up contexts for Gmail and UofS accounts
    (setq  mu4e-user-mail-address-list '("kevin.green@usask.ca"
                                         "kevin.richard.green@gmail.com"))
    (setq mu4e-contexts
          `( ,(make-mu4e-context
               :name "UofS"	  ; University of Saskatchewan account
               :enter-func (lambda () "Switch to UofS context")
               :leave-func (lambda () (setq mu4e-maildir-list nil))
               :match-func (lambda (msg) (when msg
                                           (string-prefix-p
                                            "/UofS"
                                            (mu4e-message-field msg :maildir))))
               :vars '((user-mail-address . "kevin.green@usask.ca")
                       (user-full-name . "Kevin R Green")
                       (mu4e-sent-folder . "/UofS/Sent Items")
                       (mu4e-drafts-folder . "/UofS/Drafts")
                       (mu4e-trash-folder . "/UofS/Deleted Items")
                       (mu4e-refile-folder . "/UofS/Archive")
                       ))
             ,(make-mu4e-context
               :name "Gmail"
               :enter-func (lambda () "Switch to Gmail context")
               :leave-func (lambda () (setq mu4e-maildir-list nil))
               :match-func (lambda (msg) (when msg ; anything with /Gmail in the path
                                           (string-prefix-p
                                            "/Gmail"
                                            (mu4e-message-field msg :maildir))))
               :leave-func (lambda () (mu4e-clear-caches))
               :vars '((user-mail-address . "kevin.richard.green@gmail.com")
                       (user-full-name . "Kevin R Green")
                       (mu4e-sent-folder . "/Gmail/[Gmail].All Mail")
                       (mu4e-drafts-folder . "/Gmail/[Gmail].Drafts")
                       (mu4e-trash-folder . "/Gmail/[Gmail].Trash")
                       (mu4e-refile-folder . "/Gmail/[Gmail].All Mail")))
             ))

    ;; the maildirs you use frequently; access them with 'j' ('jump')
    (setq   mu4e-maildir-shortcuts
            '(("/UofS/INBOX"         . ?U)
              ("/Gmail/INBOX"        . ?G)))

    ;; (setq mu4e-get-mail-command "offlineimap -o")

    ;; Send mail with externally configured msmtp program.
    (setq message-send-mail-function 'message-send-mail-with-sendmail
          sendmail-program "/usr/bin/msmtp")

    ;; This enabled the thread like viewing of email similar to gmail's UI.
    (setq mu4e-headers-include-related t)

    (setq mu4e-attachment-dir  "~/Downloads/mu4e")
    (setq mu4e-update-interval nil)
    ;; inline images
    (setq mu4e-view-show-images t)
    ;; Use imagemagick, if available.
    (when (fboundp 'imagemagick-register-types)
      (imagemagick-register-types))

    ;; Sometimes html email is just not readable in a text based client,
    ;; this lets me open the email in my browser.
    (add-to-list 'mu4e-view-actions '("View in browser" . mu4e-action-view-in-browser) t)

    ;; spell checking
    (add-hook 'mu4e-compose-mode-hook 'flyspell-mode)

    ;; Use ivy for mu4e completions
    (setq mu4e-completing-read-function 'ivy-completing-read)

    ;; This hook correctly modifies the \Inbox and \Starred flags on email when they are marked.
    ;; Without it refiling (archiving) and flagging (starring) email won't properly result in
    ;; the corresponding gmail action.
    (add-hook 'mu4e-mark-execute-pre-hook
              (lambda (mark msg)
                (cond ((member mark '(refile trash)) (mu4e-action-retag-message msg "-\\Inbox"))
                      ((equal mark 'flag) (mu4e-action-retag-message msg "\\Starred"))
                      ((equal mark 'unflag) (mu4e-action-retag-message msg "-\\Starred")))))

    ;; Use the correct account context when sending mail based on the from header.
    (setq message-sendmail-envelope-from 'header)

    (defun choose-msmtp-account ()
      (if (message-mail-p)
          (save-excursion
            (let*
                ((from (save-restriction
                         (message-narrow-to-headers)
                         (message-fetch-field "from")))
                 (account
                  (cond
                   ((string-match "kevin.richard.green@gmail.com" from) "Gmail")
                   ((string-match "kevin.green@usask.ca" from) "UofS"))))
              (setq message-sendmail-extra-arguments (list '"-a" account))))))
    (add-hook 'message-send-mail-hook 'choose-msmtp-account)

    (setq mu4e-headers-visible-lines 20)

    ;; Add date to reply quotes
    (use-package mu-cite
      :config
      (setq mu4e-compose-cite-function 'mu-cite-original)
      (setq mu-cite-top-format '("On " date ",\n" from " wrote:\n\n"))
      (setq mu-cite-prefix-format '("> ")))
    )
#+END_SRC
